;Archivo:		PostLab06.s
;Dispositivo:		PIC16F887
;Autor:			Marian López
;Programa:		Muestra números aumentando a 1 seg en 2 displays 
;Hardware:	        2 displays 2 segmentos en PORTB
;			Circuitos selectores en RD0 y RD1
;Creado:		2 marzo, 2022
;Última modificación:	5 marzo, 2022

PROCESSOR 16F887
    
; PIC16F887 Configuration Bit Settings

; Assembly source line config statements

; CONFIG1
  CONFIG  FOSC = INTRC_NOCLKOUT ; Oscillator Selection bits (INTOSC oscillator: CLKOUT function on RA6/OSC2/CLKOUT pin, I/O function on RA7/OSC1/CLKIN)
  CONFIG  WDTE = OFF            ; Watchdog Timer Enable bit (WDT disabled and can be enabled by SWDTEN bit of the WDTCON register)
  CONFIG  PWRTE = ON            ; Power-up Timer Enable bit (PWRT enabled)
  CONFIG  MCLRE = OFF           ; RE3/MCLR pin function select bit (RE3/MCLR pin function is digital input, MCLR internally tied to VDD)
  CONFIG  CP = OFF              ; Code Protection bit (Program memory code protection is disabled)
  CONFIG  CPD = OFF             ; Data Code Protection bit (Data memory code protection is disabled)
  CONFIG  BOREN = OFF           ; Brown Out Reset Selection bits (BOR disabled)
  CONFIG  IESO = OFF            ; Internal External Switchover bit (Internal/External Switchover mode is disabled)
  CONFIG  FCMEN = OFF           ; Fail-Safe Clock Monitor Enabled bit (Fail-Safe Clock Monitor is disabled)
  CONFIG  LVP = ON              ; Low Voltage Programming Enable bit (RB3/PGM pin has PGM function, low voltage programming enabled)

; CONFIG2
  CONFIG  BOR4V = BOR40V        ; Brown-out Reset Selection bit (Brown-out Reset set to 4.0V)
  CONFIG  WRT = OFF             ; Flash Program Memory Self Write Enable bits (Write protection off)

// config statements should precede project file includes.
#include <xc.inc>

; MACROS
RESET_TMR0 MACRO TMR_VAR	; FUNCIÓN QUE RESETEA EL TMR0 CON CUALQUIER VALOR
    BANKSEL TMR0		
    MOVLW   TMR_VAR
    MOVWF   TMR0		; TIEMPO DE RETARDE
    BCF	    T0IF		; LIMPIA BANDERA DE INTERRUPCIÓN
    ENDM
  
RESET_TMR1 MACRO TMR1_H, TMR1_L ; FUNCIÓN QUE RESETEA EL TMR1 CON CUALQUIER VALOR
    MOVLW   TMR1_H
    MOVWF   TMR1H		; CARGA EL BYTE MAS SIGNIFICATIVO
    MOVLW   TMR1_L
    MOVWF   TMR1L		; CARGA EL BYTE MENOS SIGNIFICATIVO
    BCF     TMR1IF		; LIMPIA LA BANDERA DE INTERRUPCIÓN
    ENDM
    
; VARIABLES
PSECT udata_shr			; MEMORIA COMPARTIDA
    W_TEMP:	    DS 1
    STATUS_TEMP:    DS 1
    
PSECT udata_bank0		; MEMORIA DE BANCO 0
    VALOR:	    DS 1	; VALOR A MOSTRAR EN DISPLAYS
    BANDERA:	    DS 1	; INDICA QUÉ DISPLAY ESTÁ ENCENDIDO
    NIBBLES:	    DS 2	; GUARDA EL NIBBLE ALTO Y BAJO
    DISPLAY:	    DS 2	; GUARDA VALORES BINARIOS RECUPERADOS DE TABLA
    
; VECTOR RESET
PSECT RESVECT, CLASS=CODE, ABS, DELTA=2
ORG 00H				
RESETVECT:
    PAGESEL MAIN		
    GOTO    MAIN
    
; VECTOR INTERRUPCIONES
PSECT INTVECT, CLASS=CODE, ABS, DELTA=2
ORG 04H				; POSICIÓN 04H PARA INTERRUPCIONES
PUSH:
    MOVWF   W_TEMP		; GUARDA W
    SWAPF   STATUS, W   
    MOVWF   STATUS_TEMP		; GUARDA REG STATUS
	
ISR:
    BTFSC   TMR1IF		; FUE INTERRUPCIÓN DE TMR1? No=0 Si=1
    CALL    INT_TMR1
    BTFSC   T0IF		; FUE INTERRUPCIÓN DEL TMR0? No=0 Si=1
    CALL    INT_TMR0
    
POP:
    SWAPF   STATUS_TEMP, W  
    MOVWF   STATUS		; RECUPERA REG STATUS
    SWAPF   W_TEMP, F	    
    SWAPF   W_TEMP, W		; RECUPERA W
    RETFIE			; REGRESA A CICLO PRINCIPAL

; SUBRUTINAS DE INTERRUPCIÓN
INT_TMR0:
    RESET_TMR0 251		; REINICIA TMR0 PARA 10 MS
    CALL    MOSTRAR_VALOR
    RETURN  
    
INT_TMR1:
    RESET_TMR1 11100001B, 01111100B ; REINICIA TMR1 PARA 1 SEG
    INCF    VALOR		; INCREMENTA VARIABLE
    RETURN
    
; CONFIGURACIÓN
PSECT CODE, DELTA=2, ABS
ORG 80H	
MAIN:
    CALL    CONFIG_IO		; CONFIGURACIÓN I/O
    CALL    CONFIG_RELOJ	; CONFIGURACIÓN OSCILADOR
    CALL    CONFIG_TMR1		; CONFIGURACIÓN TMR1
    CALL    CONFIG_TMR0		; CONFIGURACIÓN TMR0
    CALL    CONFIG_INT		; CONFIGURACIÓN INTERRUPCIONES

; LOOP PRINCIPAL
LOOP:
    CALL    OBTENER_NIBBLE
    CALL    SET_DISPLAY
    GOTO    LOOP
    
; SUBRUTINAS
CONFIG_IO:
    BANKSEL ANSEL
    CLRF    ANSEL		; PINES DIGITALES
    CLRF    ANSELH		; PINES DIGITALES
    BANKSEL TRISB
    CLRF    TRISB		; PORTB SALIDA
    BCF	    TRISD, 0		; RD0 SALIDA
    BCF	    TRISD, 1		; RD1 SALIDA
    BANKSEL PORTB
    CLRF    PORTB		; PORTB EN 0
    BSF	    PORTD, 0		; APAGA DISPLAY 2
    BSF	    PORTD, 1		; APAGA DISPLAY 1
    CLRF    BANDERA		; BANDERA EN 0
    RETURN   
    
CONFIG_RELOJ:
    BANKSEL OSCCON
    BCF	    SCS			; RELOJ INTERNO
    BCF	    IRCF2		
    BSF	    IRCF1		
    BCF	    IRCF0		; PRESCALER = 010 -> 250 kHz 
    RETURN
  
CONFIG_TMR0:
    BANKSEL OPTION_REG
    BCF	    T0CS		; INCREMENTO CON PULSOS DE RELOJ INTERNO
    BCF	    PSA			; PRESCALER A TMR0
    BSF	    PS2
    BSF	    PS1
    BCF	    PS0			; PRESCALER 1 : 128
    RESET_TMR0 251
    RETURN
    
CONFIG_TMR1:
    BANKSEL T1CON
    BCF	    TMR1GE		; EL TMR1 SIEMPRE CUENTA
    BSF	    T1CKPS1
    BSF	    T1CKPS0		; PRESCALER DE 1:8
    BCF	    T1OSCEN		; DESHABILITA EL OSCILADOR DE BAJO VOLTAJE
    BCF	    TMR1CS		; RELOJ INTERNO
    BSF	    TMR1ON		; PRENDE EL TMR1
    RESET_TMR1 11100001B, 01111100B ; TMR1 INTERRUPCIÓN CADA 1 SEG
    RETURN
    
CONFIG_INT:
    BANKSEL PIE1
    BSF	    TMR1IE		; INTERRUPCIÓN TMR1 HABILITADA
    BANKSEL INTCON
    BSF	    GIE			; INTERRUPCIONES GLOBALES HABILITADAS
    BSF	    PEIE		; INTERRUPCIONES DE PERIFÉRICOS HABILITADAS
    BCF     TMR1IF		; LIMPIAR BANDERA DE TMR1
    BSF	    T0IE		; INTERRUPCIÓN TMR0 HABILITADA
    BCF	    T0IF		; LIMPIAR BANDERA DE TMR0
    RETURN
   
OBTENER_NIBBLE:
    MOVLW   0x0F
    ANDWF   VALOR, W		; VALOR AND 0000 1111
    MOVWF   NIBBLES		; OBTIENE NIBBLE BAJO
    MOVLW   0xF0		
    ANDWF   VALOR, W		; VALOR AND 1111 0000
    MOVWF   NIBBLES+1		; OBTIENE NIBBLE ALTO
    SWAPF   NIBBLES+1, F	; PARA BUSCAR EN TABLA
    RETURN

SET_DISPLAY:
    MOVF    NIBBLES, W
    CALL    TABLA_7SEG		; TRANSFORMA NIBBLE BAJO A BYTE PARA DISPLAY 1
    MOVWF   DISPLAY		; GUARDA BYTE DE DISPLAY EN VARIABLE DISPLAY
    MOVF    NIBBLES+1, W	; 
    CALL    TABLA_7SEG		; TRANSFORMA NIBBLE ALTO A BYTE PARA DISPLAY 2
    MOVWF   DISPLAY+1		; GUARDA BYTE DE DISPLAY EN VARIABLE DISPLAY+1    
    RETURN
    
MOSTRAR_VALOR:
    MOVLW   0x03		; TIENE QUE EMPEZAR ENCENDIDO PORQUE ASÍ EL TRANSISTOR
    MOVWF   PORTD		; CONECTA A TIERRA Y SE APAGA EL DISPLAY DE ÁNODO COMÚN
    BTFSS   BANDERA, 0		; SI BANDERA ESTÁ ENCENDIDA SALTA (PORQUE ESTÁ ENCENDIDO EL NIBBLE ALTO)
    GOTO    DISPLAY_2
    ; MUESTRA NIBBLE BAJO EN DISPLAY 1  
    DISPLAY_1:
	MOVF    DISPLAY, W
	MOVWF   PORTB		; COLOCA EL BYTE DE DISPLAY EN PUERTO C
	BCF	PORTD, 1	; APAGA RD1 Y PRENDE DISPLAY 1
	BCF	BANDERA, 0	; APAGA BANDERA INDICANDO QUE ESTÁ ENCENDIDO EL DISPLAY 1
	RETURN
    ; MUESTRA NIBBLE ALTO EN DISPLAY 2
    DISPLAY_2:
	MOVF    DISPLAY+1, W
	MOVWF   PORTB		; COLOCA EL BYTE DE DISPLAY EN PUERTO C
	BCF	PORTD, 0	; APAGA RD1 Y PRENDE DISPLAY 2
	BSF	BANDERA, 0	; ENCIENDE BANDERA INDICANDO QUE ESTÁ ENCENDIDO EL DISPLAY 2
	RETURN
	
; TABLA ÁNODO COMÚN
ORG 200H        		; POSICIÓN PARA LA TABLA DE DISPLAY
TABLA_7SEG:
    CLRF    PCLATH		; PCLATH = 00
    BSF	    PCLATH, 1		; PCLATH = 01
    ADDWF   PCL			; PC = PCLATH + PCL + W
    RETLW   11000000B		; 0
    RETLW   11111001B		; 1
    RETLW   10100100B		; 2
    RETLW   10110000B		; 3
    RETLW   10011001B		; 4
    RETLW   10010010B		; 5
    RETLW   10000010B		; 6
    RETLW   11111000B		; 7
    RETLW   10000000B		; 8
    RETLW   10010000B		; 9
    RETLW   10001000B		; A
    RETLW   10000011B		; B
    RETLW   11000110B		; C
    RETLW   10100001B		; D
    RETLW   10000110B		; E
    RETLW   10001110B		; F

